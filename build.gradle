apply from: file("${rootDir}/gradle/project.gradle")

subprojects {
    group = 'com.lgzzzz'
    version = '1.0.0'

    repositories {
        maven {
            url 'https://neowu.github.io/maven-repo/'
            content {
                includeGroup 'core.framework'
            }
        }
    }

}

def coreNGVersion = '7.4.1'
def mysqlVersion = '8.0.20'
def mongoVersion = '7.4.1'
def hsqlVersion = '2.5.0'


configure(subprojects.findAll { it.name.endsWith('-db-migration') }) {
    apply from: file("${rootDir}/gradle/db-migration.gradle")

    dependencies {
        runtimeOnly "mysql:mysql-connector-java:${mysqlVersion}"
    }
}

configure(subprojects.findAll { it.name.endsWith('-mongo-migration') }) {
    apply from: file("${rootDir}/gradle/app.gradle")
    dependencies {
        implementation "core.framework:core-ng:${coreNGVersion}"
        implementation "core.framework:core-ng-mongo:${coreNGVersion}"
    }
    tasks.register('mongoMigrate') {
        dependsOn run
    }
}

configure(subprojects.findAll { it.name.matches('.*-interface') }) {
    dependencies {
        implementation "core.framework:core-ng-api:${coreNGVersion}"
    }
}

// services use core-ng
configure([
        project(':admin-service'),
        project(':borrow-record-service'),
        project(':book-service'),
        project(':back-office'),
        project(':website'),
        project(':notification-service'),
        project(':scheduler-service'),
        project(':user-service'),
        project(':authentication-service'),
]) {
    dependencies {
        implementation "core.framework:core-ng:${coreNGVersion}"
        testImplementation "core.framework:core-ng-test:${coreNGVersion}"
    }
}

// services use mongo
configure([project(':borrow-record-service')]) {
    dependencies {
        implementation "core.framework:core-ng-mongo:${mongoVersion}"
    }
}

// services use db
configure([
        project(':admin-service'),
        project(':book-service'),
        project(':notification-service'),
        project(':user-service'),
]) {
    dependencies {
        runtimeOnly "mysql:mysql-connector-java:${mysqlVersion}"
        testRuntimeOnly "org.hsqldb:hsqldb:${hsqlVersion}"
    }
}

project(':book-service') {
    dependencies {
        implementation project(':book-service-interface')
        implementation project(':borrow-record-service-interface')
    }
}

project(':borrow-record-service') {
    dependencies {
        implementation project(':borrow-record-service-interface')
    }
}

project(':user-service') {
    dependencies {
        implementation project(':user-service-interface')
    }
}

project(':notification-service') {
    dependencies {
        implementation project(':notification-service-interface')
    }
}

project(':admin-service') {
    dependencies {
        implementation project(':admin-service-interface')
    }
}

project(':scheduler-service') {
    dependencies {
        implementation project(':book-service-interface')
        implementation project(':borrow-record-service-interface')
    }
}

project(':authentication-service') {
    dependencies {
        implementation project(':authentication-service-interface')
        implementation project(':user-service-interface')
        implementation project(':admin-service-interface')
    }
}

project(':back-office') {
    dependencies {
        implementation project(':back-office-interface')
        implementation project(':admin-service-interface')
        implementation project(':book-service-interface')
        implementation project(':borrow-record-service-interface')
        implementation project(':user-service-interface')
        implementation project(':authentication-service-interface')
    }
}

project(':website') {
    dependencies {
        implementation project(':website-interface')
        implementation project(':book-service-interface')
        implementation project(':notification-service-interface')
        implementation project(':authentication-service-interface')
    }
}